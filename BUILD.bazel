load("@aspect_rules_ts//ts:defs.bzl", "ts_project")
load("@npm//:defs.bzl", "npm_link_all_packages")
load("@npm//:playwright/package_json.bzl", playwright_bin = "bin")
load("@npm//:vite/package_json.bzl", vite_bin = "bin")
load("@npm//:vitest/package_json.bzl", vitest_bin = "bin")

npm_link_all_packages()

ts_project(
    name = "compile",
    transpiler = "tsc",
    no_emit = True,
    srcs = glob([
        "src/**/*.ts",
        "src/**/*.tsx",
    ]),

    deps = [
        "//:node_modules/@testing-library/dom",
        "//:node_modules/@testing-library/jest-dom",
        "//:node_modules/@testing-library/react",
        "//:node_modules/@types/react",
        "//:node_modules/@types/react-dom",
        "//:node_modules/react",
        "//:node_modules/react-dom",
        "//:node_modules/vite",
        "//:node_modules/vitest",
    ],
)

vite_bin.vite(
    name = "build",
    srcs = [
        ":index.html",
        ":vite.config.ts",

        "//:node_modules/react",
        "//:node_modules/react-dom",
        "//:node_modules/vite",
        "//:node_modules/@vitejs/plugin-react",
    ] + glob([
        "public/**",
        "src/**/*.css",
        "src/**/*.ts",
        "src/**/*.tsx",
    ]),
    out_dirs = ["dist"],
    chdir = package_name(),
    args = ["build"],
)

vite_bin.vite_binary(
    name = "preview",
    args = ["preview"],
    data = [":build"],
)

playwright_bin.playwright(
    name = "install_browsers",
    out_dirs = ["browsers"],
    chdir = package_name(),
    args = ["install"],
    env = { "PLAYWRIGHT_BROWSERS_PATH": "browsers" },
)

vitest_bin.vitest_test(
    name = "test",
    args = ["run"],
    data = [
        ":install_browsers",
        ":vitest.setup.ts",
        ":vitest.config.ts",

        "//:node_modules/@testing-library/dom",
        "//:node_modules/@testing-library/jest-dom",
        "//:node_modules/@testing-library/react",
        "//:node_modules/react",
        "//:node_modules/react-dom",
        "//:node_modules/vitest",
        "//:node_modules/@vitejs/plugin-react",
        "//:node_modules/@vitest/browser-playwright",
    ] + glob([
        "src/**/*.ts",
        "src/**/*.tsx",
    ]),
    env = { "PLAYWRIGHT_BROWSERS_PATH": "browsers" },
)
